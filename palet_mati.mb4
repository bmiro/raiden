10 ''''''''''''''''''''''''Capçalera requerida a l'enunciat'''''''''''''''''''''''
20 def INTE GROC, BLANC, BLAU, VERMELL 
30 VERMELL = 1
40 BLANC = 2
50 GROC = 3
60 BLAU = 4

70 def INTE N 'veure variable pcsPila de la seccio de declaracions propies
80 N = 2
90 dim tipusP!(4) 'veure la variable TP de la seccio de declaracions propies
100 tipusP(1) = BLANC
110 tipusP(2) = BLAU
120 tipusP(3) = GROC
130 tipusP(4) = VERMELL

140 'coordenades x i y del punt P respecte la camera
150 def FLOAT oPx, oPy
160 oPx = 100.0
170 oPy = 530.0
180 'angle de rotació del palé respecte la camera
190 def FLOAT angle
200 angle = 0.0
210 'coordenades x i y de l’origen del sistema de la camera
220 def FLOAT cPx, cPy
230 cPx = 0.0
240 cPy = 0.0

250 ''''''''''''''''''''''''     Macros pròpies     ''''''''''''''''''''''''
260 def INTE PINCA
270 PINCA = 1

280 def FLOAT DOPINCAI, DOPINCAF, DTPINCAI, DTPINCAF 'emps abans i despres per obrir/tancar pinça
290 DOPINCAI = 1.0 'Es important estar ben estatic el deixar
300 DOPINCAF = 0.5
310 DTPINCAI = 0.5
320 DTPINCAF = 0.5

330 def INTE VLENT, VNORMAL
340 VLENT = 10
350 VNORMAL = 20

360 def FLOAT ZS, ZTPT, ZTPR 'Pla Z on es desplaça el robot per sobre de la terra _real_
370 ZS = 280.0
380 ZTPT = ZS - 6.0 ' Pla de terra amb pinça tombada
390 ZTPR = ZS + 6.5  ' Pla de terra amb pinça recta


400 def INTE TP 'tipus de pença diferents
410 TP = 4

420 def INTE PILES 'Numero de pilaes inicials on hi ha les peces
430 PILES = 2

440 def INTE PCSPILA 'numero de peces de cada pila
450 PCSPILA = N

460 def INTE PALS 'Numero de pals
470 PALS = 4

480 def FLOAT DT 'Diferencial de temps per la pinça
490 DT = 0.5

500 def FLOAT HDISC 'Altura dels discs
510 HDISC = 8.5

520 'TODO documentar cap on intrementen les x i tot l'entorn, premises de colocacio etc.
530 def FLOAT DXPILES 'Distancia X entre les pilaes
540 DXPILES = 95.0
550 def FLOAT DYPILES 'Distancia Y entre les pilaes
560 DYPILES = 0.0
570 def FLOAT DXPALE  'Distancia X entre els punts del pale
580 DXPALE  = 110.0
590 def FLOAT DYPALE  'Distancia Y entre els punts del pale
600 DYPALE = 110.0
610 def FLOAT DXPALS  'Dinstancia X entre els pals
620 DXPALS  = 70.0
630 def FLOAT DYPALS  'Dinstancia Y entre els pals
640 DYPALS  = 0.0
650 def FLOAT DZPAL   'Dinstancia Z per entrar dins el pal
660 DZPAL  = 35.0

670 dim RELPALE# (4,2) '4 = TP, posicions relatives al centre per fer el palé (x, y)
680 RELPALE (1,1) = -55.0 
690 RELPALE (1,2) =  55.0
700 RELPALE (2,1) =  55.0 
710 RELPALE (2,2) =  55.0
720 RELPALE (3,1) =  55.0 
730 RELPALE (3,2) = -55.0
740 RELPALE (4,1) = -55.0 
750 RELPALE (4,2) = -55.0 

760 ''''''''''''''''''''''''     Declaracions pròpies     ''''''''''''''''''''''''
770 dim npcsPila!(2) '2 = PILES
780 npcsPila(1) = N
790 npcsPila(2) = N

800 dim ordre!(4) '4 = TP Ordre de recollida de les peces

810 dim alloc!(4) '4 = TP Tantes posicions com tipus de peça, les peces que ja estan a lloc

820 def POS Prvsnl 'Punt Provisional per calcul

830 dim Pale(4) '4 = TP, posicio segura sobra la columna d'on s'ha de posar la peça
840 dim PaleOut(4)
850 dim Pila(2) '2 = PILES posicio segura sobre la pila
860 dim PalI(4) ' 4 = PALS
870 dim PalF(4) ' 4 = PALS

880 def INTE i, pil, pal, munt, peca, pecapila, tipus 'iteradors

890 def FLOAT ppx, ppy 'punt pale  ' emprats com a paramentres per el pals de 
900 def FLOAT prx, pry 'punt robot ' coorenades del pale al robot

910 ''''''''''''''''''''''''''''''''''   MAIN   '''''''''''''''''''''''''''''''''''
920 gosub *INIT
930 gosub *CALCPTS
940 mov Pal0
950 gosub *DESPALE
960 end

970 ''''''''''''''''''''''''''''''''    RUTINES   '''''''''''''''''''''''''''''''''
980 *INIT
990    servo ON
1000    ovrd 10
1010    mov Paralisi
1020    gosub *OPINCA
1030    for i = 1 to TP
1040        alloc(i) = 0
1050    next
1060    return

1070 *CALCPTS
1080    for pil = 1 to PILES
1090        Pila(pil) = Pila0
1100        Pila(pil).x = Pila(1).x + DXPILES * (pil - 1)
1110        Pila(pil).y = Pila(1).y + DYPILES * (pil - 1) 'es 0
1120    next
    
1140    for tipus = 1 to TP
1150        ppx = RELPALE(tipus, 1) 'paramentres de entrada de CAM2ROB
1160        ppy = RELPALE(tipus, 2)
1170        gosub *CAM2ROB           'prx i pry son parametres de sortida
1180        Pale(tipus) = Pale0    'Escriu altres components i orientacions
1190        Pale(tipus).x = prx
1200        Pale(tipus).y = pry
1210        PaleOut(tipus) = PaleOut0
1220        PaleOut(tipus).x = prx + 5.0
1230        PaleOut(tipus).y = pry - 20.0
1240    next
    
1260    select angle
1270    case 45.0 to 135.0
1280        ordre(1) = 3
1290        ordre(2) = 4
1300        ordre(3) = 1
1310        ordre(4) = 2
1320    case 135.0 to 225.0
1330        ordre(1) = 4
1340        ordre(2) = 2
1350        ordre(3) = 3
1360        ordre(4) = 1
1370    case 225.0 to 315.0
1380        ordre(1) = 2
1390        ordre(2) = 1
1400        ordre(3) = 4
1410        ordre(4) = 3
1420    default ' 315.0 > angle < 45.0
1430        ordre(1) = 1
1440        ordre(2) = 3
1450        ordre(3) = 2
1460        ordre(4) = 4
1470    end select
    
1500    for pal = 1 to PALS
1510        PalI(pal) = Pila0
1520        PalI(pal).x = PalI(pal).x + DXPALS * (pal - 1)
1530        PalI(pal).y = PalI(pal).y + DYPALS * (pal - 1)
1540        PalF(pal) = PalI(pal)
1550        PalF(pal).z = PalF(pal).z - DZPAL
1570    next
1580    return

1590 *ACABA
1600    mov Paralisi
1610    hopen PINCA
1620    servo OFF
1630    return

1640 *CAM2ROB
1650    prx = ppx*sin(angle) + ppy*cos(angle) + oPy + cPx
1660    pry = ppx*cos(angle) - ppy*sin(angle) + oPx + cPy
1670    return
    
1690 *OPINCA
1700    dly DOPINCAI
1710    hopen PINCA
1720    dly DOPINCAF
1730    return

1740 *TPINCA
1750    dly DTPINCAI
1760    hclose PINCA
1770    dly DTPINCAF
1780    return

1790 *MNTPALE
1800    peca = 1
1810    for pil = 1 to PILES
1820        for pecapila = 1 to PCSPILA
1830            gosub *AGAFPILA
1840            gosub *POSAPALE
1850            peca = peca + 1
1860        next
1870    next
1880    return
    
1900 *DESPALE
1910    for tipus = 1 to TP
1920        munt = ordre(tipus)
1930        peca = 1
1940        while peca < alloc(munt) + 1
1950            gosub *AGAFPALE
1960            gosub *POSADEST
1970        wend
1980    next
1990    return

2000 *AGAFPILA
2010    mov Pila(pil)
2020    Prvsnl = Pila(pil)
2030    Prvsnl.z = Prvsnl.z - ZTPR + (npcspila!(pil) - pecapila) * HDISC
2040    ovrd VLENT
2050    mvs Prvsnl
2060    gosub *TPINCA
2070    mvs Pila(pil)
2080    ovrd VNORMAL
2090    return

2100 *POSAPALE
2110    mov Pale(tipusP(peca))
2120    Prvsnl = Pale(tipusP(peca))
2130    Prvsnl.z = Prvsnl.z - ZTPR + alloc(tipusP(peca)) * HDISC
2140    ovrd VLENT
2150    mvs Prvsnl
2160    gosub *OPINCA
2170    mvs Pale(tipusP(peca))
2180    ovrd VNORMAL
2190    alloc(tipusP(peca)) = alloc(tipusP(peca)) + 1
2200    return

2210 *AGAFPALE
2220    mov PaleOut(munt)
2230    Prvsnl = PaleOut(munt)
2240    Prvsnl.z = Prvsnl.z - ZTPT + (alloc(munt) - 1) * HDISC
2250    ovrd VLENT
2260    mvs Prvsnl
2270    gosub *TPINCA
2280    mvs PaleOut(munt)
2290    ovrd VNORMAL
2300    alloc(munt) = alloc(munt) - 1
2310    return

2320 *POSADEST
2330    if (tipus < TP) then 'Agafam les 3 primeres
2340       mov PalI(munt)
2350       ovrd VLENT
2360       mvs PalF(munt)
2370       gosub *OPINCA
2380       mvs PalI(munt)
2390       ovrd VNORMAL
2400    else
2410       mov Paralisi
2420       gosub *OPINCA
2430    endif
2440    return
